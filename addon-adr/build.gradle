plugins{
    id 'distribution'
}

ext.addonName = "ActionDurationReminder"
ext.addonId = 1536
ext.addonCompatible = '10.0.0'
file('changelog').withReader('utf8'){reader->
    def m = reader.readLine() =~ /(\d+)(\.\w+)+/
    if(m){
        def ver = m[0][0]
        ext.addonVersion = ver
        ver = ver.replaceAll('a','.1').replaceAll('b','.2').replaceAll('c','.3')
        def verNo = m[0][1].padLeft(2,'0')
        def mm = ver=~ /\.(\w+)/
            verNo += mm[0][1].padLeft(3,'0')
            verNo += mm.size() ==2? mm[1][1] :''
        ext.addonVersionNumber = verNo.padRight(6,'0')
    }else{
        ext.addonVersion = 'SNAPSHOT'
        ext.addonVersionNumber = '000000'
    }
}

CopySpec mainContents = copySpec {
    filteringCharset = 'iso8859-1'
    from (addonName){
        exclude '**/*.dds'
         filter{
            it.replaceAll('@@ADDON_VERSION@@',addonVersion).replaceAll('@@ADDON_VERSION_NO@@',addonVersionNumber)
        }
    }
    from (addonName){
        include '**/*.dds'
    }
}

distTar.enabled = false
distributions {
    main {
        distributionBaseName  = addonName
        contents {
        	with mainContents
        }
    }
}

distZip{
    archiveFile.set(file("${buildDir}/distributions/${addonName}-${addonVersion}.zip"))
}

if (hasProperty('ESO_PTS_ADDONS_DIR')){
    def installDir =  new File(project.property( 'ESO_PTS_ADDONS_DIR'),addonName)
    task installToPTS(type:Sync){
    	group 'distribution'
    	description 'Installs the project into PTS dir'
    	with mainContents
        into installDir
    }
}

if (hasProperty('ESO_LIVE_ADDONS_DIR')){
    def installDir =  new File(project.property( 'ESO_LIVE_ADDONS_DIR'),addonName)
    task installToLive(type:Sync){
    	group 'distribution'
    	description 'Installs the project into LIVE dir'
    	with mainContents
        into installDir
    }
}

task upload {
    group = "distribution"
    description = "upload addon zip file to esoui.com"
    dependsOn distZip
    doLast{
        def changelog = file('changelog').getText('utf8').replaceAll('"',/\\"/)
        def cmd = ['curl',
            '-H', "\"x-api-token: ${project.property('com.esoui.apiToken')}\"",
            '-F', "id=${addonId}",
            '-F', "version=$addonVersion",
            '-F', "compatible=$addonCompatible",
            '-F', "\"changelog=$changelog\"",
            '-F', "\"updatefile=@${distZip.archiveFile.get().asFile.absolutePath}\"",
            //'--resolve', 'api.esoui.com:443:104.25.180.72',
            'https://api.esoui.com/addons/update']
        //print cmd.join(' ')
        def proc = cmd.execute()
        proc.consumeProcessOutput(System.out, System.err)
        proc.waitFor()
    }
}

task addonInfo{
    group = "help"
    description = "show addon info"
    doLast{
        def proc = ['curl',
            '-H', "\"x-api-token: ${project.property('com.esoui.apiToken')}\"",
            "https://api.esoui.com/addons/details/${addonId}.json"]
                   .execute()
        proc.consumeProcessOutput(System.out, System.err)
        proc.waitFor()
    }
}
